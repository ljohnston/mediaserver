
on logIt(msg)
    -- TODO: Log rotation.
    set logFile to "{{ automount_logfile }}"
    set now to current date
    set d to short date string of now
    set t to time string of now
    try
        open for access logFile with write permission
        write "[" & d & " " & t & "] " & msg & linefeed starting at eof to logFile
    end try
    try
        close access logFile
    end try
end logIt

on serverExists()
    try
        do shell script "nc -G2 -z {{ mediaserver_hostname }} {{ smbd_port }} &>/dev/null"
        return true
    on error
        my logIt("Smb service on '{{ mediaserver_hostname }}' not responding (server down?)")
        return false
    end try
end serverExists

tell application "Finder"
    set isConnected to disk "{{ media_share }}" exists
end tell

if isConnected = false and serverExists() then
    my logIt("Volume '{{ media_share }}' not mounted. Mounting...")
    try
        -- This "mount volume" command will throw up a dialog on every attempt
        -- if the server doesn't exist (hence the serverExists() check).
        mount volume "{{ media_share }}" on server "{{ mediaserver_hostname }}"
        my logIt("Volume '{{ media_share }}' successfully mounted")
    on error errMsg
        my logIt("Error mounting volume '{{ media_share }}' (" & errMsg & ")")
    end try
end if
